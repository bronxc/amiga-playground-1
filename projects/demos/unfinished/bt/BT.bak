	SECTION	"Frantic - BITCH TREATLE",CODE_C

;--------------------------------------;

WaitBlt	MACRO
	tst.b	2(a5)
.WB\@	btst	#6,2(a5)
	bne.b	.WB\@
	ENDM

;--------------------------------------;

Start	move.l	4.w,a6
	moveq	#0,d0
	lea	GfxBase(pc),a1
	jsr	-552(a6)
	move.l	d0,GfxBase
	move.l	d0,a6
	move.l	34(a6),OldView
	sub.l	a1,a1
	jsr	-222(a6)
	jsr	-270(a6)
	jsr	-270(a6)
	jsr	-456(a6)
	lea	$dff000,a5
	WaitBlt
	move.l	4.w,a6
	jsr	-132(a6)

	move.w	$1c(a5),d0
	or.w	#$c000,d0
	move.w	d0,IntEna

	move.w	2(a5),d0
	or.w	#$8000,d0
	move.w	d0,DMACon

	move.l	#BTData,d0
	lea	BPLBT(pc),a0
	moveq	#3-1,d7
.BT	move.w	d0,6(a0)
	swap	d0
	move.w	d0,2(a0)
	swap	d0
	add.l	#54*(320/8),d0
	addq.l	#8,a0
	dbf	d7,.BT

	move.l	#Screen1,d0
	lea	BPL1(pc),a0
	moveq	#1-1,d7
.2	move.w	d0,6(a0)
	swap	d0
	move.w	d0,2(a0)
	swap	d0
	add.l	#200*(320/8),d0
	addq.l	#8,a0
	dbf	d7,.2

	move.w	$7c(a5),d0
	cmp.b	#$f8,d0
	bne.b	.NonAGA
	move.w	#$0000,$106(a5)
	move.w	#$0011,$10c(a5)
	move.w	#$0000,$1fc(a5)

.NonAGA	bsr.w	.GetVBR
	lea	$dff000,a5
	move.w	#$7fff,$9a(a5)
	move.w	#$7fff,$9c(a5)
	move.l	$6c(a4),OldIRQ
	move.l	#NewIRQ,$6c(a4)
	move.w	#$c020,$9a(a5)

	move.w	#$7fff,$96(a5)
	move.l  #CList,$80(a5)
	move.w	#$0000,$88(a5)
	move.w	#$83d0,$96(a5)
	move.l	#0,$144(a5)

.Mouse	btst	#6,$bfe001
	bne.b	.Mouse

	lea	$dff000,a5
	move.w	#$7fff,$9a(a5)
	move.w	#$7fff,$9c(a5)
	move.l	OldIRQ(pc),$6c(a4)
	move.w	IntEna(pc),$9a(a5)

	WaitBlt
	move.l	GfxBase(pc),a6
	move.l	OldView(pc),a1
	jsr	-222(a6)
	move.w	#$7fff,$96(a5)
	move.l	38(a6),$80(a5)
	move.w	#$0000,$88(a5)
	move.w	DMACon(pc),$96(a5)
	move.l	a6,a1
	jsr	-462(a6)
	move.l	4.w,a6
	jsr	-414(a6)
	jsr	-138(a6)
	moveq	#0,d0
	rts

.GetVBR	sub.l	a4,a4
	move.l	4.w,a6
	btst	#0,297(a6)
	beq.w	.VBR1
	lea	.VBR2(pc),a5
	jsr	-30(a6)
.VBR1	rts

.VBR2	dc.w	$4e7a,$c801
	rte

;--------------------------------------;

NewIRQ	movem.l	d0-a6,-(a7)
	btst	#2,$dff016
	beq	Pause
	bsr	Draw

Pause	movem.l	(a7)+,d0-a6
;	move.w	#$00f0,$180(a5)
	move.w	#$0020,$9c(a5)
	rte

;--------------------------------------;

;	move.l	#64,d0
;	move.l	#110<<16,d1
;	divs.l	d0,d1
;	rts

Draw	WaitBlt
	move.w	#(320/8),$60(a5)
	move.w	#(320/8),$66(a5)
	move.w	#$8000,$74(a5)
	move.w	#$ffff,$44(a5)

	moveq	#40,d0
	moveq	#0,d1
	move.w	yop(pc),d1
	move.l	#10,d2
	moveq	#35,d3
	addq.w	#1,yop
	lea	Gfx(pc),a0
	lea	Screen1(pc),a1
	bsr	DrawLne
	rts

yop	dc.w	0

;--------------------------------------;

DrawLne	moveq	#0,d4
	sub.w	d1,d3
	roxl.b	#1,d4
	tst.w	d3
	bge	.1
	neg.w	d3
.1	sub.w	d0,d2
	roxl.b	#1,d4
	tst.w	d2
	bge	.2
	neg.w	d2
.2	move.w	d3,d5
	sub.w	d2,d5
	bge	.3
	exg	d2,d3
.3	roxl.b	#1,d4
	move.b	OctTble(pc,d4),d4

	add.w	d2,d2
	move.w	d2,$62(a5)
	sub.w	d3,d2
	bge	.4
	or.b	#$40,d4
.4	move.w	d2,$52(a5)
	sub.w	d3,d2
	move.w	d2,$64(a5)

	move.w	d4,$42(a5)

	move.w	d0,d2
	and.w	#$fff0,d2
	lsr.w	#3,d2
	lea	(a1,d2),a2
	mulu	#(320/8),d1
	lea	(a2,d1),a2
	move.l	a2,$48(a5)
	move.l	a2,$54(a5)
	and.w	#$f,d0
	ror.w	#4,d0
	or.w	#$bca,d0
	move.w	d0,$40(a5)
						;d2=LDelta,d3=SDelta
	moveq	#(128/16)-1,d7
	move.w	#16<<6+(32/16),d0
.5	move.w	(a0)+,$72(a5)			;mask
	move.w	d0,$58(a5)
	WaitBlt
	dbf	d7,.5
	rts

OctTble	dc.b	0<<2+1,4<<2+1,2<<2+1,5<<2+1
	dc.b	1<<2+1,6<<2+1,3<<2+1,7<<2+1

;--------------------------------------;

DMACon	dc.w	0
IntEna	dc.w	0
OldIRQ	dc.l	0
OldView	dc.l	0
GfxBase	dc.b	"graphics.library",0,0

;--------------------------------------;

CList	dc.w	$008e,$2c81,$0090,$2cc1
	dc.w	$0092,$0038,$0094,$00d0
	dc.w	$0102,$0000,$0104,$003f
	dc.w	$0108,$0000,$010a,$0000

	dc.w	$0180,$0000,$0182,$0531
	dc.w	$0184,$0742,$0186,$0864
	dc.w	$0188,$0a86,$018a,$0ca9
	dc.w	$018c,$0edc,$018e,$0fff

BPLBT	dc.w	$00e0,$0000,$00e2,$0000
	dc.w	$00e4,$0000,$00e6,$0000
	dc.w	$00e8,$0000,$00ea,$0000
	dc.w	$2c07,$fffe,$0100,$3200

	dc.w	$6207,$fffe,$0100,$0200
	dc.w	$0180,$0000,$0182,$0fff

BPL1	dc.w	$00e0,$0000,$00e2,$0000
;	dc.w	$00e4,$0000,$00e6,$0000
;	dc.w	$00e8,$0000,$00ea,$0000
;	dc.w	$00ec,$0000,$00ee,$0000
;	dc.w	$00f0,$0000,$00f2,$0000
;	dc.w	$00f4,$0000,$00f6,$0000
;	dc.w	$00f8,$0000,$00fa,$0000
;	dc.w	$00fc,$0000,$00fe,$0000
	dc.w	$6407,$fffe,$0100,$1200

	dc.w	$ffe1,$fffe
	dc.w	$2c07,$fffe,$0100,$0200

	dc.w	$ffff,$fffe,$ffff,$fffe

;--------------------------------------;

Gfx	dcb.b	128*(128/8),$ff
BTData	incbin	"DH1:BT.raw"
Screen1	dcb.b	2*200*(320/8),0

	END

